class PasswordDatabase is subclass of GLOBAL
instance variables
    private passwords: seq of Password := [];
    private eqState: LockState := <Unlocked>;

    inv forall password in seq passwords & password.GetMatchString() <> nil;
operations   

    public Unlock: EncryptionKey_t ==> ()
        Unlock(encKey) == 
        ( for all i in set inds passwords do
            passwords(i).Decrypt(encKey);
        let isUnlocked = {password.GetEncryptionState() | 
                          password in seq passwords} 
        in
            if(card isUnlocked = 1 and <PlainText> in set isUnlocked or 
            len passwords = 0) then
                    eqState  := <Unlocked>)
        pre eqState = <Locked> and forall password in seq passwords & 
                password.GetEncryptionState() = <Encrypted>
        post eqState = <Unlocked> and forall password in seq passwords & 
                password.GetEncryptionState() = <PlainText>;
    
    public Lock: EncryptionKey_t ==> ()
        Lock(encKey) == 
        (for all i in set inds passwords do
            passwords(i).Encrypt(encKey);
            eqState := <Locked>)
        pre eqState = <Unlocked> and forall password in seq passwords & 
                password.GetEncryptionState() = <PlainText>
        post eqState = <Locked> and forall password in seq passwords & 
                password.GetEncryptionState() = <Encrypted>;

    public pure GetLockState: () ==> LockState
        GetLockState() ==
        return eqState;

    public AddPassword: Password ==> ()
        AddPassword(password) == if password.GetMatchString() <> nil then
            passwords := passwords ^ [password] 
        else 
            io.print("Password needs to have a match" ^
                     " string to be added to the database.\n")
        pre eqState = <Unlocked> and password.GetEncryptionState() = <PlainText>
        post password.GetMatchString() <> nil => len passwords = len passwords~ + 1;

    public GetAllPasswords: () ==> seq of Password
        GetAllPasswords() == return passwords;

    public FindPasswords: String ==> seq of Password
        FindPasswords(match) == 
            (dcl ret : seq of Password := [];
                for all i in set inds passwords do
                    if passwords(i).GetMatchString() = match then
                        ret := ret ^ [passwords(i)];
                return ret;
            )
        pre eqState = <Unlocked>;

    public RemovePassword: nat ==> ()
        RemovePassword(idx) == if idx <= len passwords then
            passwords := [passwords(k) | k in set inds passwords & k <> idx]
        pre eqState = <Unlocked>;
end PasswordDatabase
